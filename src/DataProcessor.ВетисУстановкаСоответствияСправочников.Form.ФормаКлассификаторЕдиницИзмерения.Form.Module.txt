
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеСервер Тогда
		Элементы.СправочникСписок.Видимость = Ложь;
	#Иначе
		Элементы.СписокВетисКонтекстноеМенюКомандаСоздатьСвязь.Видимость = Ложь;
		Элементы.СписокВетисКонтекстноеМенюКомандаУдалитьСвязь.Видимость = Ложь;
	#КонецЕсли
	
	СправочникСписок.ПроизвольныйЗапрос = Истина;
	СправочникСписок.ОсновнаяТаблица = ВетисИмяСправочника.Unit("Справочник");
	СправочникСписок.ТекстЗапроса =
	"ВЫБРАТЬ
	|	_Таблица.Ссылка,
	|	_Таблица.Код,
	|	_Таблица.Наименование КАК name,
	|	_Ветис.guid КАК guid,
	|	_Ветис.name КАК Ветисname,
	|	_Ветис.Описание КАК ВетисОписание
	|{ВЫБРАТЬ
	|	Ссылка.*,
	|	Код,
	|	name,
	|	guid,
	|	Ветисname,
	|	ВетисОписание}
	|ИЗ
	|	Справочник."+ВетисИмяСправочника.Unit()+" КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _Ветис
	|		ПО _Таблица.Ссылка = _Ветис.Ссылка
	|ГДЕ
	|	ИСТИНА
	|	И НЕ _Таблица.ПометкаУдаления
	|	И ИСТИНА";
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаНайти(Команда)
	
	КомандаНайтиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздатьСвязь(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		
		_Параметры = Новый Структура("guid, name", Элементы.СписокВетис.ТекущиеДанные.guid, Элементы.СписокВетис.ТекущиеДанные.name);
		
		ВетисФормы.UnitФормаВыбора(ЭтаФорма, "КомандаСоздатьСвязьВыборОбработкаОповещения",,,_Параметры);
		
	#Иначе
		
		Если Элементы.СправочникСписок.ТекущиеДанные = Неопределено ИЛИ Элементы.СписокВетис.ТекущаяСтрока = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		_ТекущиеДанныеВетис = СписокВетис.НайтиПоИдентификатору(Элементы.СписокВетис.ТекущаяСтрока);
		
		Для Каждого _ТекущиеДанные1сСсылка из Элементы.СправочникСписок.ВыделенныеСтроки Цикл
			ВетисВызовСервера.Соответствие_Добавить(_ТекущиеДанные1сСсылка, _ТекущиеДанныеВетис.guid, _ТекущиеДанныеВетис.name);
		КонецЦикла;
		
		Элементы.СправочникСписок.Обновить();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьСвязь(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		
		ВетисВызовСервера.Соответствие_Удалить(Элементы.СписокВетис.ТекущиеДанные.guid, ВетисИмяСправочника.Unit("СправочникСсылка"));
		
	#Иначе
		
		Для Каждого _ТекущиеДанные из Элементы.СправочникСписок.ВыделенныеСтроки Цикл
			ВетисВызовСервера.Соответствие_Удалить(_ТекущиеДанные, "СправочникСсылка.КлассификаторЕдиницИзмерения");
		КонецЦикла;
		
		Элементы.СправочникСписок.Обновить();
	#КонецЕсли
	
КонецПроцедуры


&НаСервере
Процедура КомандаНайтиСервер()
	
	Перем _Item, _Параметры;
	
	_ВетисDictionaryService = ?(Ветис.Версия_2_0(), ВетисDictionaryService_2_0, ВетисDictionaryService);
	
	СписокВетис.Очистить();
	
	Пока _ВетисDictionaryService.GetUnitListСледующий(_Item, _Параметры) Цикл
		
		// только базовые
		Если НЕ _Item.guid = _Item.commonUnitGuid Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаСтрока = СписокВетис.Добавить();
		ТаблицаСтрока.name = _Item.name;
		ТаблицаСтрока.guid = _Item.guid;
		
	КонецЦикла;
	
	СписокВетис.Сортировать("name");
	
КонецПроцедуры
