
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеСервер Тогда
		Элементы.ОтборПредприятие.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборХС.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.СписокВСДАннулировать.Видимость = Ложь;
		Элементы.ГруппаОтборUuid.Видимость = Ложь;
	#КонецЕсли
	
	СкладскойЖурналПоследние = Истина;
	
	НастроитьУсловноеОформление();
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурнал.Отбор, "last", СкладскойЖурналПоследние,,СкладскойЖурналПоследние);
	
	Элементы.СписокСкладскойЖурналСкладскойЖурналПоследние.Пометка = СкладскойЖурналПоследние;
	
	Версия = Ветис.Версия();
	
	ОтборХС = Ветис.Настройки_ОсновнойХС();
	
	ОтборСписокВСДТип.Добавить(Перечисления.ВетисVetDocumentType.Транспортный,,Истина);
	ОтборСписокВСДТип.Добавить(Перечисления.ВетисVetDocumentType.Возвратный,,Истина);
	ОтборСписокВСДТип.Добавить(Перечисления.ВетисVetDocumentType.Производственный);
	//ОтборСписокВСДТип.Добавить(Перечисления.ВетисVetDocumentType.Входящий);
	//ОтборСписокВСДТип.Добавить(Перечисления.ВетисVetDocumentType.Исходящий);
	
	ОтборСписокВСДСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Оформлен,,Истина);
	ОтборСписокВСДСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Погашен);
	ОтборСписокВСДСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Аннулирован);
	
	ОтборСписокВходящиеПартииСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Оформлен,,Истина);
	ОтборСписокВходящиеПартииСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Погашен);
	ОтборСписокВходящиеПартииСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Аннулирован);
	
	ОтборСписокТранспортныеПартииСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Оформлен,,Истина);
	ОтборСписокТранспортныеПартииСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Погашен,,Истина);
	ОтборСписокТранспортныеПартииСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Аннулирован);
	
	Элементы.ГруппаСкладскойЖурнал.Видимость = Ветис.РазрешенаОтладка();
	
	Элементы.СписокВСДuuid.Видимость = Ветис.Настройки_ПоказыватьGuid();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Ветис.ИнициализироватьНастройкиХС(ОтборХС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		АвтоЗаголовок = Истина;
	#КонецЕсли
	
	ВетисВызовСервера.ТекущееПредприятие(ОтборПредприятие);
	
	ОбновитьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисНастройки" Тогда
		Версия = ВетисВызовСервера.Версия();
		Отладка = ВетисВызовСервера.РазрешенаОтладка();
		Элементы.ГруппаСкладскойЖурнал.Видимость = Отладка;
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		Версия = ВетисВызовСервера.Версия();
	ИначеЕсли ИмяСобытия = "ВетисОтладка" Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисТестовый" Тогда
		//
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаЖурнал(Команда)
	
	ОткрытьФорму("РегистрСведений.ВетисЖурнал.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастройки(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ВетисНастройки");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановкаСоответствияСправочников(Команда)
	
	ОткрытьФорму("Обработка.ВетисУстановкаСоответствияСправочников.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредприятияХС(Команда)
	
	ОткрытьФорму("РегистрСведений.ВетисПредприятияХС.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНаправление(Команда)
	
	Направление = НЕ Направление;
	
	ОбновитьОтборы("ОтборНаправление");
	
КонецПроцедуры



&НаКлиенте
Процедура КомандаАннулироватьВСД(Команда)
	
	_ТекущиеДанные = Элементы.СписокВСД.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ _ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Оформлен") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ _ТекущиеДанные.Тип = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentType.Транспортный") Тогда
		Возврат;
	КонецЕсли;
	
	_Причина = "";
	
	_оо = Новый ОписаниеОповещения("ВСДАннулироватьОбработкаОповещения", ЭтаФорма, _ТекущиеДанные.Ссылка);
	
	ПоказатьВводСтроки(_оо, _Причина, "Причина аннулирования " + _ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСинхронизироватьВСД(Команда)
	
	Перем _Отказ, _Ошибка;
	
	Если НЕ ЗначениеЗаполнено(ОтборХС) Тогда
		Сообщить("Необходимо выбрать ХС");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОтборПредприятие) Тогда
		Сообщить("Необходимо выбрать предприятие");
		Возврат;
	КонецЕсли;
	
	//СинхронизироватьВСДНаСервере(_Отказ, _Ошибка);
	
	ВетисХозяйственныеОперации.СинхронизироватьЖурналВСДИзменения(ОтборХС, ОтборПредприятие, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
		Возврат;
	КонецЕсли;
	
	Элементы.СписокВСД.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДАннулироватьОбработкаОповещения(Результат, Параметр) Экспорт
	
	Перем _Отказ, _Ошибка;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВетисMercuryVetdocumentСлой1с.Аннулировать(Параметр, Результат, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	Иначе
		Элементы.СписокВСД.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтборСписокВСДТип(Команда)
	
	ОтборСписокВСДТип.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("ОтборСписокВСДТипОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписокВСДТипОбработкаОповещения(пСписок, пПараметры) Экспорт
	
	Если НЕ пСписок = Неопределено Тогда
		ОбновитьОтборы("ОтборСписокВСДТип");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтборСписокВСДСтатус(Команда)
	
	ОтборСписокВСДСтатус.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("ОтборСписокВСДСтатусОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписокВСДСтатусОбработкаОповещения(пСписок, пПараметры) Экспорт
	
	Если НЕ пСписок = Неопределено Тогда
		ОбновитьОтборы("ОтборСписокВСДСтатус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтборСписокВходящиеПартииСтатус(Команда)
	
	ОтборСписокВходящиеПартииСтатус.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("ОтборСписокВходящиеПартииСтатусОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписокВходящиеПартииСтатусОбработкаОповещения(пСписок, пПараметры) Экспорт
	
	Если НЕ пСписок = Неопределено Тогда
		ОбновитьОтборы("ОтборСписокВходящиеПартииСтатус");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтборСписокТранспортныеПартииСтатус(Команда)
	
	ОтборСписокТранспортныеПартииСтатус.ПоказатьОтметкуЭлементов(Новый ОписаниеОповещения("ОтборСписокТранспортныеПартииСтатусОбработкаОповещения", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписокТранспортныеПартииСтатусОбработкаОповещения(пСписок, пПараметры) Экспорт
	
	Если НЕ пСписок = Неопределено Тогда
		ОбновитьОтборы("ОтборСписокТранспортныеПартииСтатус");
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьОтборы(пИмяСобытия = Неопределено)
	
	Если пИмяСобытия = "ОтборСписокВСДТип" ИЛИ пИмяСобытия = Неопределено Тогда
		
		_заголовок = "";
		
		_список = Новый Массив;
		_список.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentType.ПустаяСсылка"));
		
		Для каждого _элемент Из ОтборСписокВСДТип Цикл
			Если _элемент.Пометка Тогда
				_список.Добавить(_элемент.Значение);
				_заголовок = _заголовок + "," + Строка(_элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "Тип", _список, "ВСписке");
		
		Элементы.СписокВСДОтборТип.Заголовок = Сред(_заголовок, 2);
	КонецЕсли;
	
	
	Если пИмяСобытия = "ОтборСписокВСДСтатус" ИЛИ пИмяСобытия = Неопределено Тогда
		
		_заголовок = "";
		
		_список = Новый Массив;
		_список.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка"));
		
		Для каждого _элемент Из ОтборСписокВСДСтатус Цикл
			Если _элемент.Пометка Тогда
				_список.Добавить(_элемент.Значение);
				_заголовок = _заголовок + "," + Строка(_элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "Статус", _список, "ВСписке");
		
		Элементы.СписокВСДОтборСтатус.Заголовок = Сред(_заголовок, 2);
	КонецЕсли;
	
	
	Если пИмяСобытия = "ОтборСписокВходящиеПартииСтатус" ИЛИ пИмяСобытия = Неопределено Тогда
		
		_заголовок = "";
		
		_список = Новый Массив;
		_список.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка"));
		
		Для каждого _элемент Из ОтборСписокВходящиеПартииСтатус Цикл
			Если _элемент.Пометка Тогда
				_список.Добавить(_элемент.Значение);
				_заголовок = _заголовок + "," + Строка(_элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВходящиеПартииПоДокументам.Отбор, "Статус", _список, "ВСписке");
		
		Элементы.СписокВходящиеПартииПоДокументамОтборСтатус.Заголовок = Сред(_заголовок, 2);
	КонецЕсли;
	
	Если пИмяСобытия = "ОтборСписокТранспортныеПартииСтатус" ИЛИ пИмяСобытия = Неопределено Тогда
		
		_заголовок = "";
		
		_список = Новый Массив;
		_список.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка"));
		
		Для каждого _элемент Из ОтборСписокТранспортныеПартииСтатус Цикл
			Если _элемент.Пометка Тогда
				_список.Добавить(_элемент.Значение);
				_заголовок = _заголовок + "," + Строка(_элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокТранспортныеПартии.Отбор, "Статус", _список, "ВСписке");
		
		Элементы.СписокТранспортныеПартииОтборСтатус.Заголовок = Сред(_заголовок, 2);
	КонецЕсли;
	
	
	Если пИмяСобытия = "ОтборХСПредприятие" ИЛИ пИмяСобытия = Неопределено Тогда
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурнал.Отбор, "ХозяйствующийСубъект", ОтборХС);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурнал.Отбор, "Предприятие", ОтборПредприятие);
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурналОстатки.Отбор, "ХозяйствующийСубъект", ОтборХС);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурналОстатки.Отбор, "Предприятие", ОтборПредприятие);
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокТранспортныеПартии.Отбор, "Отправитель", ОтборХС);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокТранспортныеПартии.Отбор, "ОтправительПредприятие", ОтборПредприятие);
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВходящиеПартииПоДокументам.Отбор, "Получатель", ОтборХС);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВходящиеПартииПоДокументам.Отбор, "ПолучательПредприятие", ОтборПредприятие);
	КонецЕсли;
	
	
	Если пИмяСобытия = "ОтборХСПредприятие" ИЛИ пИмяСобытия = "ОтборНаправление" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.СписокВСДКомандаНаправление.Заголовок = ?(Направление, "Иходящие", "Входящие");
		
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "Отправитель", ОтборХС, , Направление);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "Получатель", ОтборХС, , НЕ Направление);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "ОтправительПредприятие", ОтборПредприятие, , Направление);
		ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "ПолучательПредприятие", ОтборПредприятие, , НЕ Направление);
		
		Элементы.СписокВСДОтправитель.Видимость = НЕ Направление;
		Элементы.СписокВСДПолучатель.Видимость = Направление;
		Элементы.СписокВСДОтправительПредприятие.Видимость = НЕ Направление;
		Элементы.СписокВСДПолучательПредприятие.Видимость = Направление;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборUuidИспользованиеПриИзменении(Элемент)
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "uuid", ОтборUuid, , ОтборUuidИспользование);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСинхронизироватьСкладскойЖурнал(Команда)
	
	Перем _Отказ, _Ошибка;
	
	ВетисХозяйственныеОперации.СинхронизироватьСкладскойЖурнал(ОтборХС, ОтборПредприятие, , _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
		Возврат;
	КонецЕсли;
	
	ВетисХозяйственныеОперации.СинхронизироватьСкладскойЖурналОстатки_20(ОтборХС, ОтборПредприятие, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
		Возврат;
	КонецЕсли;
	
	Элементы.СписокСкладскойЖурнал.Обновить();
	Элементы.СписокСкладскойЖурналОстатки.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСкладскойЖурналОтборПоследние(Команда)
	
	СкладскойЖурналПоследние = НЕ СкладскойЖурналПоследние;
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурнал.Отбор, "last", СкладскойЖурналПоследние,,СкладскойЖурналПоследние);
	
	Элементы.СписокСкладскойЖурналСкладскойЖурналПоследние.Пометка = СкладскойЖурналПоследние;
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)
	
	ВетисВызовСервера.ВерсияУстановить(Версия);
	
	ВетисВызовСервера.ИнициализироватьXSD();
	
	Оповестить("ВетисВерсия", ЭтаФорма.ИмяФормы);
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСкладскойЖурналТест(Команда)
	
	ТекущиеДанные = Элементы.СписокСкладскойЖурнал.ТекущиеДанные;
	
	СкладскойЖурналПолучитьТекущийСервер(ТекущиеДанные.guid);
	
КонецПроцедуры

Процедура СкладскойЖурналПолучитьТекущийСервер(guid)
	//тест:
	//http://vetrf.ru/vetrf-forum/posts/list/1185/6855.page#46389
	//В записи журнала есть объект vetDocument, в котором возвращаются реквизиты ВСД. То есть при выполнении метода, например, GetStockEntryByGuid вы получите данные записи журнала и UUID ветеринарного сертификата, на основании которого эта запись была создана. 
	//результат:
	//работает только в 2.0
	
	Если Ветис.Версия_2_0() Тогда
		_enterpriseGuid = ВетисDictionaryСлой1с.Enterprise(ОтборПредприятие);
		_issuerId       = ВетисDictionaryСлой1с.BusinessEntity(ОтборХС);
		_StockEntry     = ВетисMercuryApplications_2_0.getStockEntryByGuid(guid, _enterpriseGuid, _issuerId);
		
		Если _StockEntry.vetDocument.Количество() > 0 Тогда
			_VetDocument    = ВетисMercuryApplications_2_0.getVetDocumentByUuid(_StockEntry.vetDocument[0].uuid, _enterpriseGuid, _issuerId);
		КонецЕсли;
	Иначе
		_enterpriseGuid = ВетисCerberusEnterpriseСлой1с.Enterprise(ОтборПредприятие);
		_issuerId       = ВетисCerberusEnterpriseСлой1с.BusinessEntity(ОтборХС);
		_StockEntry     = ВетисMercuryApplications.getStockEntryByGuid(guid, _enterpriseGuid, _issuerId);
		
		Если _StockEntry.vetDocument.Количество() > 0 Тогда
			_VetDocument    = ВетисMercuryApplications.getVetDocumentByUuid(_StockEntry.vetDocument[0].uuid, _enterpriseGuid, _issuerId);
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.СписокСкладскойЖурналСкладскойЖурналПолучитьТекущий.Заголовок = "Тест "+_StockEntry.vetDocument.Количество();
	
КонецПроцедуры


&НаКлиенте
Процедура ОтборПредприятиеПриИзменении(Элемент)
	
	ВетисВызовСервера.ТекущееПредприятие(ОтборПредприятие);
	
	ОбновитьОтборы("ОтборХСПредприятие");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "ОтборПредприятиеНачалоВыбораОбработкаОповещения", СтандартнаяОбработка, ОтборПредприятие, ОтборХС);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПредприятиеНачалоВыбораОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		ОтборПредприятие = пРезультат;
		ВетисВызовСервера.ТекущееПредприятие(ОтборПредприятие);
		ОбновитьОтборы("ОтборХСПредприятие");
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтборХСПриИзменении(Элемент)
	
	ВетисВызовСервера.ИнициализироватьНастройкиХС(ОтборХС);
	
	ОбновитьОтборы("ОтборХСПредприятие");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.BusinessEntityФормаВыбора(ЭтаФорма, "ОтборХСНачалоВыбораОбработкаОповещения", СтандартнаяОбработка, ОтборХС, Новый Структура("Свои"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХСНачалоВыбораОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		ОтборХС = пРезультат;
		ОтборПредприятие = Неопределено;
		ВетисВызовСервера.ИнициализироватьНастройкиХС(ОтборХС);
		ОбновитьОтборы("ОтборХСПредприятие");
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура СинхронизироватьВСДНаСервере(пОтказ, пОшибка)
	
	_Параметры = Новый Структура("Входящие, Исходяще, Тип, Статус", Истина, Ложь);
	
	Для каждого _Статус Из ОтборСписокВСДСтатус Цикл
		
		Если НЕ _Статус.Пометка Тогда Продолжить; КонецЕсли;
		
		_Параметры.Статус = _Статус.Значение;
		
		Для каждого _Тип Из ОтборСписокВСДТип Цикл
			
			Если НЕ _Тип.Пометка Тогда Продолжить; КонецЕсли;
			
			_Параметры.Тип = _Тип.Значение;
			
			ВетисХозяйственныеОперации.СинхронизироватьЖурналВСД(ОтборХС, ОтборПредприятие, _Параметры, пОтказ, пОшибка);
			
			Если пОтказ = Истина Тогда
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура НастроитьУсловноеОформление()
	
	НастроитьУсловноеОформлениеДобавить(Новый Цвет(255, 255, 153), "Оформлен", "Транспортный");
	НастроитьУсловноеОформлениеДобавить(Новый Цвет(204, 255, 255), "Оформлен", "Возвратный");
	НастроитьУсловноеОформлениеДобавить(WebЦвета.Розовый, "Аннулирован");
	НастроитьУсловноеОформлениеДобавить(Новый Цвет(204, 255, 204), "Погашен");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьУсловноеОформлениеДобавить(пЦвет, пСтатус = Неопределено, пТип = Неопределено)
	
	_уо = УсловноеОформление.Элементы.Добавить();
	
	_уо.Использование = Истина;
	
	_уо.Оформление.УстановитьЗначениеПараметра("ЦветФона", пЦвет);
	
	Если НЕ пСтатус = Неопределено Тогда
		_отбор = _уо.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		_отбор.Использование = Истина;
		_отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокВСД.Статус");
		_отбор.ПравоеЗначение = Перечисления.ВетисVetDocumentStatus[пСтатус];
	КонецЕсли;
	
	Если НЕ пТип = Неопределено Тогда
		_отбор = _уо.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		_отбор.Использование = Истина;
		_отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокВСД.Тип");
		_отбор.ПравоеЗначение = Перечисления.ВетисVetDocumentType[пТип];
	КонецЕсли;
	
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДДата");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДНомер");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДДатаВСД");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДНомерВСД");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДОтправитель");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДОтправительПредприятие");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДПолучатель");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДПолучательПредприятие");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДНоменклатурнаяГруппа");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДНоменклатура");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДКоличество");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДuuid");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДКомментарий");
	НастроитьУсловноеОформлениеДобавитьПоле(_уо, "СписокВСДОтветственныйВетис");
	
КонецПроцедуры

Процедура НастроитьУсловноеОформлениеДобавитьПоле(пУО, пИмяПоля)
	
	_поле = пУО.Поля.Элементы.Добавить();
	_поле.Использование = Истина;
	_поле.Поле = Новый ПолеКомпоновкиДанных(пИмяПоля);
	
КонецПроцедуры
