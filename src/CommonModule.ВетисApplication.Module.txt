
// тип, описывающий cведения о содержимом заявки
Функция Application(пОбъект, пИмя) Экспорт
	
	_Объект = Создать("Application");
	
	_Объект.serviceId = Ветис.serviceId();
	_Объект.issuerId  = Ветис.issuerId();
	_Объект.issueDate = Ветис.ВремяСервиса();//xs:dateTime
	_Объект.data      = ApplicationDataWrapper(пОбъект, пИмя);
	
	Возврат _Объект;
	
КонецФункции

// результат обработки заявки
Функция ApplicationResult(пОбъект) Экспорт
	
	Возврат пОбъект.result;
	
КонецФункции

// тип, описывающий cведения о содержимом заявки
Функция ApplicationDataWrapper(пОбъект = Неопределено, пИмя = Неопределено) Экспорт
	
	_Объект = Создать("ApplicationDataWrapper");
	
	Если НЕ пОбъект = Неопределено Тогда
		
		_Объект.Добавить(ФормаXML.Элемент, пОбъект.Тип().URIПространстваИмен, пИмя, пОбъект);
		
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

// тип, описывающий cведения о результате обработки заявки
Функция ApplicationResultWrapper(пОбъект) Экспорт
	
	Возврат пОбъект.applicationResultData;
	
КонецФункции


#Область Service

// Заявка в единой заявочной системе на выполнение определенной операции,
// определяет пользователя, который инициирует запрос и содержит сведения самого запроса.
Функция submitApplicationRequest(пApplication = Неопределено) Экспорт
	
	_Объект = Создать("submitApplicationRequest", ServiceURI());
	
	_Объект.apiKey      = Ветис.apiKey();
	_Объект.application = пApplication;
	
	Возврат _Объект;
	
КонецФункции

// Заявка в заявочной системе, содержит сведения запроса.
Функция submitApplicationResponse(пОбъект) Экспорт
	
	Возврат пОбъект.application;
	
КонецФункции

// Запрос на получение результатов обработки заявки.
Функция receiveApplicationResultRequest(applicationId) Экспорт
	
	_Объект = Создать("receiveApplicationResultRequest", ServiceURI());
	
	_Объект.apiKey        = Ветис.apiKey();
	_Объект.issuerId      = Ветис.issuerId();
	_Объект.applicationId = applicationId;
	
	Возврат _Объект;
	
КонецФункции

// Получение результатов обработки заявки.
Функция receiveApplicationResultResponse(пОбъект) Экспорт
	
	Возврат пОбъект.application;
	
КонецФункции

#КонецОбласти


Функция Создать(пИмя, пURI = Неопределено, пФабрика = Неопределено) Экспорт
	
	Возврат ВетисОбщегоНазначения.Создать(пИмя, ?(пURI = Неопределено, URI(), пURI), ?(пФабрика = Неопределено, Фабрика(), пФабрика));
	
КонецФункции

Функция ServiceURI()
	
	Возврат "http://api.vetrf.ru/schema/cdm/application/ws-definitions";
	
КонецФункции

Функция URI() Экспорт
	
	Возврат "http://api.vetrf.ru/schema/cdm/application";
	
КонецФункции

Функция Фабрика()
	
	Возврат Ветис.Определения("ApplicationManagementService").ФабрикаXDTO;
	
КонецФункции

